grammar org.gemoc.agro.ActivitiesDSL with org.eclipse.xtext.common.Terminals

generate activitiesDSL "http://www.gemoc.org/agro/ActivitiesDSL"

Model:
	cultures+=Culture*
	resourceKinds+=ResourceKind*;

ResourceKind:
	'resource' name=ID;

Culture:
	'culture' name=ID '{' activities+=ExploitationActivity* '}';

ExploitationActivity:
	'activity' name=ID (frequency=Frequency)? ('from ' startDate=Date 'to' endDate=Date)? ('[' predicates+=Predicate ('&&'
	predicates+=Predicate)* ']')? ('using' uses+=ActivityResource ('and' uses+=ActivityResource)*)?;

ActivityResource:
	quantity=INT resourceKind=[ResourceKind];

Predicate:
	NoRain | TempOfTheDay | DelaySinceActivy | GrainIs;

NoRain:
	'no rain since' days=INT 'days';

TempOfTheDay:
	'temperature' comparison=Comp lowerTempBound=INT unit=TempUnit;

DelaySinceActivy:
	'after' (prerequisite=[ExploitationActivity]) ('is done since' days=INT 'days')?;

GrainIs:
	'grain is' state=STRING;

Date:
	day=INT month=Month;

enum Month:
	jan='jan' | feb='feb' | mar='mar' | apr='apr' | may='may' | jun='jun' | jul='jul' | aug='aug' | sept='sept' |
	oct='oct' | nov='nov' | dec='dec';

enum Comp:
	moreThan='>' | lessThan='<';

enum TempUnit:
	celsius='°C' | farenheit='°F';

enum Frequency:
	once='once' | daily='daily' | weekly='weekly' | monthly='monthly' | quaterly='quaterly' | yearly='yearly';